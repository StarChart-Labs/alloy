plugins {
    id 'org.starchartlabs.flare.multi-module-library' version '1.0.0'
    id 'com.autonomousapps.dependency-analysis' version '0.46.0'
    
    // Plug-ins not applied at the root level, but used in sub-projects
    id 'com.jfrog.bintray' version '1.8.4' apply false
}

allprojects {
    apply plugin: 'eclipse'

    // Always download sources, to allow debugging
    eclipse {
        classpath {
            downloadSources = true
        }
    }

    repositories {
        mavenCentral()
    }
    
    check.dependsOn buildHealth
}

subprojects {
    apply plugin: 'java-library'
    apply plugin: 'checkstyle'
    apply plugin: 'jacoco'
    apply plugin: 'maven-publish'
    apply plugin: 'com.jfrog.bintray'
    
    projectMetaData {
        github {
            repository 'StarChart-Labs', 'alloy'
        }
        
        licenses {
            mit()
        }
    }

    checkstyle {
        configFile = rootProject.file('config/checkstyle/checkstyle.xml')
        configProperties = [ 'checkstyle.config.dir' : rootProject.file('config/checkstyle') ]
        toolVersion = '8.31'
    }
     
    task checkstyleAll{}
     
    tasks.withType(Checkstyle).all { checkstyleTask -> checkstyleAll.dependsOn checkstyleTask }
     
    check.dependsOn checkstyleAll

    // Setup default test behavior, including failure logging
    test {
        useTestNG() {
            useDefaultListeners = true
        }
    }
    
    // Apply module naming to all projects
    // Add LICENSE so it is included in all JARs, as well as dependent licenses, fulfilling the "distributions include license" requirement
    jar {
		manifest {
			attributes("Automatic-Module-Name": "${project.group}.${project.name  - 'alloy-'}".replaceAll("-", "."))
		}
	}
    
    tasks.withType(Jar).all {
        from("${rootDir}"){
            include 'LICENSE'
        }
        
        from("${rootDir}/dependent-licenses")
    }
	
	publishing {
        publications {
            maven(MavenPublication) {
                from components.java
            }
        }
    }
    
    // If not a remote run, publish to local only
    if(!project.hasProperty('remoteDeploy')){
        publishing {
            repositories {
                mavenLocal()
            }
        }
    }

    bintray {
        publications = [ 'maven' ]

        if(!project.hasProperty('remoteDeploy')){
            dryRun = true
        }

        pkg {
            repo = 'alloy'
            name = "${project.name}"
            userOrg = 'starchart-labs'
            licenses = ['MIT']
            vcsUrl = "${projectMetaData.scm.vcsUrl}"

            version {
                name = "${project.version}"
                desc = "${project.name} release ${project.version} final"
                released  = new Date()
                vcsTag = "${project.version}"

                gpg {
                    sign = true
                }
            }
        }
	}
    
    // TODO romeara workaround until deferred credential reading is available in flare-plugins
    bintrayUpload {
        doFirst {
            user = "${credentials.bintray.username}"
            apiKey = "${credentials.bintray.password}"
        }
    }
}
